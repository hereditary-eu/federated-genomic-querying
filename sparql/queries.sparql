################################################################################
# 1) Beacon Sequence Query
#    (Here, placeholders like PARAM_POS, PARAM_REF, and PARAM_ALT stand in for
#     the actual filter values.)
################################################################################
PREFIX : <https://w3id.org/hereditary/ontology/schema/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?chrom ?pos ?ref ?alt
WHERE {
  ?variation a :GenomicVariation ;
             :referenceBases ?ref ;
             :alternateBases ?alt ;
             :location ?seqLoc .
  ?seqLoc :sequenceInterval ?seqInt ;
          :locationChr ?chrom .
  ?seqInt :sequenceIntervalStart ?pos .

  #OPTIONAL {
  #  ?seqLoc :referenceSequence ?refName .
  #  FILTER (?refName = "GRCh38"^^rdf:PlainLiteral)
  #}

  FILTER (
    ?pos = "PARAM_POS"^^rdf:PlainLiteral &&
    ?ref = "PARAM_REF"^^rdf:PlainLiteral &&
    ?alt = "PARAM_ALT"^^rdf:PlainLiteral
  )
}


################################################################################
# 2) Beacon Range Query
#    (Placeholders: PARAM_CHROM, PARAM_START, PARAM_END)
################################################################################
PREFIX : <https://w3id.org/hereditary/ontology/schema/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?chrom ?pos ?ref ?alt
WHERE {
  ?variation a :GenomicVariation ;
             :referenceBases ?ref ;
             :alternateBases ?alt ;
             :location ?seqLoc .
  ?seqLoc :sequenceInterval ?seqInt ;
          :locationChr ?chrom .
  ?seqInt :sequenceIntervalStart ?pos .
  ?seqInt :sequenceIntervalEnd ?theEnd .

  OPTIONAL {
    ?seqLoc :referenceSequence ?refName .
    FILTER (?refName = "GRCh38"^^rdf:PlainLiteral)
  }

  FILTER (
    ?chrom = "PARAM_CHROM"^^rdf:PlainLiteral &&
    (
      ?pos = "PARAM_START"^^rdf:PlainLiteral ||
      ?theEnd = "PARAM_END"^^rdf:PlainLiteral
    )
  )
}


################################################################################
# 3) Beacon Bracket Query
#    (Placeholders: PARAM_CHROM, PARAM_START_MIN, PARAM_START_MAX, 
#     PARAM_END_MIN, PARAM_END_MAX)
################################################################################
PREFIX : <https://w3id.org/hereditary/ontology/schema/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?chrom ?pos ?ref ?alt
WHERE {
  ?variation a :GenomicVariation ;
             :referenceBases ?ref ;
             :alternateBases ?alt ;
             :location ?seqLoc .
  ?seqLoc :sequenceInterval ?seqInt ;
          :locationChr ?chrom .
  ?seqInt :sequenceIntervalStart ?pos .
  ?seqInt :sequenceIntervalEnd ?theEnd .

  FILTER (
    ?chrom = "PARAM_CHROM"^^rdf:PlainLiteral &&
    (
      (xsd:integer(?pos) >= PARAM_START_MIN && xsd:integer(?pos) <= PARAM_START_MAX) ||
      (xsd:integer(?theEnd) >= PARAM_END_MIN && xsd:integer(?theEnd) <= PARAM_END_MAX)
    )
  )
}


################################################################################
# 4) Beacon VT Query
#    (Placeholders: PARAM_INFO_VALUE for the infoValue. The key "VT" is constant.)
################################################################################
PREFIX : <https://w3id.org/hereditary/ontology/schema/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?chrom ?ref ?alt
WHERE {
  {
    SELECT ?infoKey ?infoValue ?vInfoId
    WHERE {
      ?vInfo a :Variant_Info ;
             :hasInfoKey ?infoKey ;
             :hasInfoValue ?infoValue ;
             :hasVariantId ?vInfoId .
      FILTER (
        ?infoKey = "VT"^^rdf:PlainLiteral &&
        ?infoValue = "PARAM_INFO_VALUE"^^rdf:PlainLiteral
      )
    }
  }

  ?variation a :GenomicVariation ;
             :referenceBases ?ref ;
             :alternateBases ?alt ;
             :variantInternalID ?variantId ;
             :location ?seqLoc .
  ?seqLoc :sequenceInterval ?seqInt ;
          :locationChr ?chrom .

  FILTER (?variantId = ?vInfoId)
}
